# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=mydumper
pkgver=0.6.0
_myver=5.6.15
pkgrel=1
pkgdesc="A high performance MySQL backup tool."
arch=("i686" "x86_64")
url="https://launchpad.net/mydumper"
license=('GPL')
depends=("glib2" "pcre" "libmysqlclient")
makedepends=("cmake" "python2-sphinx")

source=("http://launchpad.net/mydumper/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz"
        "http://ftp.gwdg.de/pub/misc/mysql/Downloads/MySQL-5.6/mysql-$_myver.tar.gz"
        "mysqlinclude.patch")

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -p0 -i "$srcdir/mysqlinclude.patch"
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	sed -i 's/sphinx-build/sphinx-build2/' cmake/modules/FindSphinx.cmake
	cmake . \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DEXTRA_INCLUDE_DIR="$srcdir/mysql-$_myver/include" \
		-DMYSQL_CFLAGS="-lm"
	make VERBOSE=1
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
}

sha256sums=('aeaa685d411a58a6c52dd8253c29ba03e1829bf6ce42620cc66fed540723e48d'
            'f0ed95faf51670eb9d57ae901e03b0cda79f6c5899f0494b9ec867b1686fe4c7'
            'ae0bde53b1a22374db1d356a0fa8023d19a8d343948db420a67e889bf520c389')
