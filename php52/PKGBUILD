# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgbase=php52
pkgname=('php52'
         'php52-cgi'
         'php52-fpm'
         'php52-pear'
         'php52-curl'
         'php52-gd'
         'php52-gmp'
         'php52-ldap'
         'php52-mcrypt'
         'php52-odbc'
         'php52-pgsql'
         'php52-pspell'
         'php52-snmp'
         'php52-sqlite'
         'php52-tidy'
         'php52-xsl')

pkgname=php52
pkgver=5.2.17
pkgrel=9
_suhosinver=5.2.16-0.9.7
_fpmversion=0.5.14
pkgdesc='A high-level scripting language with FastCGI Process Manager'
arch=('i686' 'x86_64')
license=('PHP')
url='http://php-fpm.org/'
makedepends=('postgresql-libs' 'libmysqlclient'
             'libldap' 'postfix' 'libpng' 'libjpeg' 'sqlite3' 'unixodbc'
             'net-snmp' 'mhash' 'gmp' 'libmcrypt' 'tidyhtml' 'aspell' 'libtool'
             'freetype2' 'curl' 'libxslt' 'pam' 'openssl' 'bzip2' 'gdbm' 'db')
source=("http://www.php.net/distributions/php-${pkgver}.tar.bz2"
        "http://download.suhosin.org/suhosin-patch-${_suhosinver}.patch.gz"
        "http://php-fpm.org/downloads/php-${pkgver}-fpm-${_fpmversion}.diff.gz"
        'php.ini'
        'php-pcre.patch'
        'libpng14.patch'
        'php52-libxml.patch'
        'php-fpm.conf'
        'logrotate.d.php52-fpm'
        'php52-fpm.service'
        'php52-fpm.tmpfiles')

prepare() {
	cd "${srcdir}/php-${pkgver}"

	# libpng 1.4 fix
	patch -p3 -i "${srcdir}/libpng14.patch"

	# gmp 5.0 fix
	sed -i 's/__GMP_BITS_PER_MP_LIMB/GMP_LIMB_BITS/g' ext/gmp/gmp.c

	# apply suhosin patch
	patch -p1 -i "${srcdir}/suhosin-patch-${_suhosinver}.patch"

	# apply fpm patch
	patch -p1 -i "${srcdir}/php-${pkgver}-fpm-${_fpmversion}.diff"

	# pcre fix
	patch -p0 -i "${srcdir}/php-pcre.patch"

	# libxml2 fix
	patch -p1 -i "${srcdir}/php52-libxml.patch"
}

build() {
	phpconfig="--srcdir=../php-${pkgver} \
	--prefix=/opt/php52 \
	--sysconfdir=/etc/php52 \
	--localstatedir=/var \
	--with-layout=GNU \
	--with-config-file-path=/etc/php52 \
	--with-config-file-scan-dir=/etc/php52/conf.d \
	--enable-inline-optimization \
	--disable-debug \
	--disable-rpath \
	--disable-static \
	--enable-shared \
	--without-pear \
	--with-libdir=lib \
	"

	phpextensions="--enable-bcmath=shared \
	--enable-calendar=shared \
	--enable-dbase=shared \
	--enable-dba=shared \
	--enable-exif=shared \
	--enable-ftp=shared \
	--enable-gd-native-ttf \
	--enable-json=shared \
	--enable-mbregex \
	--enable-mbstring=all \
	--enable-mbstring=shared \
	--enable-pdo=shared \
	--enable-posix=shared \
	--enable-session=shared \
	--enable-shmop=shared \
	--enable-soap=shared \
	--enable-sockets=shared \
	--enable-sqlite-utf8 \
	--enable-sysvmsg=shared \
	--enable-sysvsem=shared \
	--enable-sysvshm=shared \
	--enable-xml \
	--enable-zip=shared \
	--with-bz2=shared \
	--with-curl=shared \
	--with-freetype-dir=shared,/usr \
	--with-gdbm=shared \
	--with-gd=shared \
	--with-gettext=shared \
	--with-gmp=shared \
	--with-iconv=shared \
	--without-imap \
	--without-imap-ssl \
	--with-jpeg-dir=shared,/usr \
	--with-ldap=shared \
	--with-mcrypt=shared \
	--with-mhash=shared \
	--with-mime-magic=shared \
	--with-mysqli=shared \
	--with-mysql=shared \
	--with-mysql=shared \
	--with-mysql-sock=/run/mysqld/mysqld.sock \
	--with-ncurses=shared \
	--with-openssl=shared \
	--with-pcre-regex=/usr \
	--with-pdo-mysql=shared \
	--with-pdo-odbc=shared,unixODBC,/usr \
	--with-pdo-pgsql=shared \
	--with-pdo-sqlite=shared,/usr \
	--with-pgsql=shared \
	--with-png-dir=shared,/usr \
	--with-pspell=shared \
	--with-regex=php \
	--with-snmp=shared \
	--with-sqlite=shared \
	--with-tidy=shared \
	--with-ttf=shared \
	--with-unixODBC=shared,/usr \
	--with-xmlrpc=shared \
	--with-xsl=shared \
	--with-zlib=shared \
	--without-db2 \
	--without-db3 \
	--without-db4 \
	"

	export EXTENSION_DIR=/opt/php52/modules
	export PEAR_INSTALLDIR=/opt/php52/pear
	export CPPFLAGS=""

	# php
	[ -d "${srcdir}/build-php" ] || mkdir "${srcdir}/build-php"
	cd "${srcdir}/build-php"
	[ -f configure ] || ln -s "../php-${pkgver}/configure"
	./configure ${phpconfig} \
		--disable-cgi \
		--with-readline \
		--enable-pcntl \
		${phpextensions}
	make

	# cgi, fcgi and fpm
	# reuse the previous run; this will save us a lot of time
	[ -d "${srcdir}/build-cgi" ] && rm -rf "${srcdir}/build-cgi"
	cp -a "${srcdir}/build-php" "${srcdir}/build-cgi"
	cd "${srcdir}/build-cgi"
	./configure ${phpconfig} \
		--disable-cli \
		--enable-cgi \
		--enable-fastcgi \
		--enable-fpm \
		--with-fpm-user=http \
		--with-fpm-group=http \
		${phpextensions}
	make

	# pear
	[ -d "${srcdir}/build-pear" ] && rm -rf "${srcdir}/build-pear"
	cp -a "${srcdir}/build-php" "${srcdir}/build-pear"
	cd "${srcdir}/build-pear"
	./configure ${phpconfig} \
		--disable-cgi \
		--with-readline \
		--enable-pcntl \
		--with-pear \
		${phpextensions}
	make
}


package_php52() {
	pkgdesc='An HTML-embedded scripting language'
	depends=('pcre' 'libxml2' 'bzip2' 'openssl')
	backup=('etc/php52/php.ini')

	cd "${srcdir}/build-php"
	make -j1 INSTALL_ROOT="${pkgdir}" install
	
	# install php.ini
	install -D -m644 "${srcdir}/php.ini" "${pkgdir}/etc/php52/php.ini"
	install -d -m755 "${pkgdir}/etc/php52/conf.d/"

	# remove static modules
	rm -f "${pkgdir}/opt/php52/modules/"*.a
	# remove modules provided by sub packages
	rm -f "${pkgdir}/opt/php52/modules/"{curl,gd,gmp,ldap,mcrypt,odbc,pdo_odbc,pgsql,pdo_pgsql,pspell,snmp,sqlite,pdo_sqlite,tidy,xsl}.so
}

package_php52-cgi() {
	pkgdesc='CGI and FCGI SAPI for PHP'
	depends=('php52' 'pcre' 'libxml2')

	install -D -m755 "${srcdir}/build-cgi/sapi/cgi/php-cgi" "${pkgdir}/opt/php52/bin/php-cgi"
}

package_php52-fpm() {
	pkgdesc='FastCGI Process Manager for PHP'
	depends=('php52-cgi')
	backup=('etc/php52/php-fpm.conf')
	install='php52-fpm.install'

	install -D -m644 "${srcdir}/php-fpm.conf" "${pkgdir}/etc/php52/php-fpm.conf"
	install -D -m644 "${srcdir}/logrotate.d.php52-fpm" "${pkgdir}/etc/logrotate.d/php52-fpm"
	install -d -m755 "${pkgdir}/etc/php52/fpm.d"
	install -D -m644 "${srcdir}/php52-fpm.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/php52-fpm.conf"
	install -D -m644 "${srcdir}/php52-fpm.service" "${pkgdir}/usr/lib/systemd/system/php52-fpm.service"
}

package_php52-pear() {
	pkgdesc='PHP Extension and Application Repository'
	depends=('php52' 'bash')
	backup=('etc/php52/pear.conf')

	cd "${srcdir}/build-pear"
	make -j1 install-pear INSTALL_ROOT="${pkgdir}"
	local i
	while read i; do
		[ ! -e "$i" ] || rm -rf "$i"
	done < <(find "${pkgdir}" -name '.*')
}

package_php52-curl() {
	depends=('php52' 'curl')
	pkgdesc='curl module for PHP'
	install -D -m755 "${srcdir}/build-php/modules/curl.so" "${pkgdir}/opt/php52/modules/curl.so"
}

package_php52-gd() {
	depends=('php52' 'libpng' 'libjpeg' 'freetype2')
	pkgdesc='gd module for PHP'
	install -D -m755 "${srcdir}/build-php/modules/gd.so" "${pkgdir}/opt/php52/modules/gd.so"
}

package_php52-gmp() {
	depends=('php52' 'gmp')
	pkgdesc='gmp module for PHP'
	install -D -m755 "${srcdir}/build-php/modules/gmp.so" "${pkgdir}/opt/php52/modules/gmp.so"
}

package_php52-ldap() {
	depends=('php52' 'libldap')
	pkgdesc='ldap module for PHP'
	install -D -m755 "${srcdir}/build-php/modules/ldap.so" "${pkgdir}/opt/php52/modules/ldap.so"
}

package_php52-mcrypt() {
	depends=('php52' 'libmcrypt' 'libtool')
	pkgdesc='mcrypt module for PHP'
	install -D -m755 "${srcdir}/build-php/modules/mcrypt.so" "${pkgdir}/opt/php52/modules/mcrypt.so"
}

package_php52-odbc() {
	depends=('php52' 'unixodbc')
	pkgdesc='ODBC modules for PHP'
	install -D -m755 "${srcdir}/build-php/modules/odbc.so" "${pkgdir}/opt/php52/modules/odbc.so"
	install -D -m755 "${srcdir}/build-php/modules/pdo_odbc.so" "${pkgdir}/opt/php52/modules/pdo_odbc.so"
}

package_php52-pgsql() {
	depends=('php52' 'postgresql-libs')
	pkgdesc='PostgreSQL modules for PHP'
	install -D -m755 "${srcdir}/build-php/modules/pgsql.so" "${pkgdir}/opt/php52/modules/pgsql.so"
	install -D -m755 "${srcdir}/build-php/modules/pdo_pgsql.so" "${pkgdir}/opt/php52/modules/pdo_pgsql.so"
}

package_php52-pspell() {
	depends=('php52' 'aspell')
	pkgdesc='pspell module for PHP'
	install -D -m755 "${srcdir}/build-php/modules/pspell.so" "${pkgdir}/opt/php52/modules/pspell.so"
}

package_php52-snmp() {
	depends=('php52' 'net-snmp')
	pkgdesc='snmp module for PHP'
	install -D -m755 "${srcdir}/build-php/modules/snmp.so" "${pkgdir}/opt/php52/modules/snmp.so"
}

package_php52-sqlite() {
	depends=('php52' 'sqlite3')
	pkgdesc='sqlite3 module for PHP'
	install -D -m755 "${srcdir}/build-php/modules/sqlite.so" "${pkgdir}/opt/php52/modules/sqlite.so"
	install -D -m755 "${srcdir}/build-php/modules/pdo_sqlite.so" "${pkgdir}/opt/php52/modules/pdo_sqlite.so"
}

package_php52-tidy() {
	depends=('php52' 'tidyhtml')
	pkgdesc='tidy module for PHP'
	install -D -m755 "${srcdir}/build-php/modules/tidy.so" "${pkgdir}/opt/php52/modules/tidy.so"
}

package_php52-xsl() {
	depends=('php52' 'libxslt')
	pkgdesc='xsl module for PHP'
	install -D -m755 "${srcdir}/build-php/modules/xsl.so" "${pkgdir}/opt/php52/modules/xsl.so"
}

sha256sums=('e81beb13ec242ab700e56f366e9da52fd6cf18961d155b23304ca870e53f116c'
            'aae115a318d80b3f32cedf876e7a8e4b932febb1b0c743c0b398003ebe122f91'
            '4f3442588138705bd4f8a2b976c4240b24e9d3d2644f202d2d3ea1ee62e46eec'
            '9d7336b7ac06dd38ea5b0ff7f5340aba8c946807cd7fddc3c7275ff9c8d2bab2'
            '0a2c4dfc9dd481ac141809e0ddd80bb43f078056efa16933f53de5f76f3e5c6c'
            '1c468e728aa09f2856756e797cf2ab475c9c9740a77ba122993532b94c60898d'
            '2e5a18e8cb2cc444b6648100ee6a34d7b5ed9166242f3fdbf1835832654ba6ba'
            '0cc929eb00dbb16fb8917ba42683f0299b0cbdcc2aa501ea90d5fe8c4f8c80aa'
            '6c293d117667c44dbe1ffa5a2cbbef4723401801d1f763ae27b6aa1fb8bebbcf'
            'd7848ada30878d9020aa2cb8fe07269873c6bdb4cb4547335d504c3c51433ff8'
            '8673e8ad1296614faf95efc9edf6fcc0d7eb85664be6751f5b76a0f5eb3479e2')
