# $Id: PKGBUILD 165345 2012-08-16 02:47:11Z dan $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=git-subtree
pkgver=1.8.5.3
pkgrel=1
pkgdesc="Merges and splits subtrees from your git project into subprojects and back"
arch=(i686 x86_64)
url="http://git-scm.com/"
license=('GPL2')
depends=('git')
makedepends=('asciidoc' 'xmlto')
source=("http://git-core.googlecode.com/files/git-$pkgver.tar.gz")

build() {
	cd "$srcdir/git-$pkgver/contrib/subtree"
	make prefix=/usr all doc
}

package() {
	cd "$srcdir/git-$pkgver/contrib/subtree"
	sed "s|libexec/git-core|lib/git-core|" -i Makefile
	install -dm755 "$pkgdir/usr/lib/git-core"
	make prefix=/usr DESTDIR="$pkgdir" install install-doc
}

md5sums=('57b966065882f83ef5879620a1e329ca')
