# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Aliaksiej Artamona≈≠ <aliaksiej.artamonau@gmail.com>

pkgname=ruby-compass
pkgver=0.12.7
pkgrel=1
pkgdesc="A Real Stylesheet Framework"
arch=(i686 x86_64)
url="http://compass-style.org"
license=("MIT")
depends=("ruby" "ruby-bundler")
makedepends=("rubygems")
source=("http://gems.rubyforge.org/gems/compass-${pkgver}.gem"
        "Gemfile"
        "Gemfile.lock"
        "bundler.patch")
noextract=("compass-$pkgver.gem")

package() {
	cd "$srcdir"
	local _gemdir="$(ruby -e'puts Gem.default_dir')"
	gem install --ignore-dependencies --no-user-install -i "$pkgdir$_gemdir" -n "$pkgdir"/usr/bin compass-$pkgver.gem
	rm "$pkgdir$_gemdir"/cache/compass-$pkgver.gem

	install -d -m755 "$pkgdir"/usr/share/licenses/$pkgname
	install -D "$pkgdir$_gemdir"/gems/compass-$pkgver/LICENSE.markdown \
	           "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

	cd "$pkgdir$_gemdir"/gems/compass-$pkgver
	cp "$srcdir"/Gemfile{,.lock} .
	bundle install --deployment
	patch -p1 -i "$srcdir"/bundler.patch

	rm "$pkgdir"/usr/bin/compass
	ln -s "$_gemdir"/gems/compass-$pkgver/bin/compass "$pkgdir"/usr/bin/compass
}

sha256sums=('e1bb4113fe000e3b93793c1bcac262ba942040be599f6c24fc1b6a007fb5a62b'
            'd8a24cecf7b4fcc4b8dbe414696835c62d1084ab93c411beedf8de68e4d2c24e'
            'f5ef26c000a3834c2b5e01442ea15cfc0a7b727a5f1257a9559a423f4003a94e'
            'fbee7344a019d115caddf00667e9473a4e82f2999bdff9c04ccc4aa31b979d72')
