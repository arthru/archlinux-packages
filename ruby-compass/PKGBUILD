# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Aliaksiej Artamona≈≠ <aliaksiej.artamonau@gmail.com>

pkgname=ruby-compass
pkgver=0.12.2
pkgrel=2
pkgdesc="A Real Stylesheet Framework"
arch=(i686 x86_64)
url="http://compass-style.org"
license=("MIT")
depends=("ruby" "ruby-sass" "ruby-chunky_png" "ruby-fssm")
makedepends=("rubygems")
source=("http://gems.rubyforge.org/gems/compass-${pkgver}.gem")
noextract=("compass-$pkgver.gem")

package() {
	cd "$srcdir"
	local _gemdir="$(ruby -e'puts Gem.default_dir')"
	gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" compass-$pkgver.gem
	rm "$pkgdir/$_gemdir/cache/compass-$pkgver.gem"

	install -d -m755 "$pkgdir/usr/share/licenses/$pkgname"
	install -D "$pkgdir$_gemdir/gems/compass-$pkgver/LICENSE.markdown" \
	           "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

sha256sums=('5f9784fcc40e1535b81e66e57d2e0d898f1a00099dd455a35dbb21fbb40f4a3b')
