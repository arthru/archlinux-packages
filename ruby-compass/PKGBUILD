# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Aliaksiej Artamona≈≠ <aliaksiej.artamonau@gmail.com>

pkgname=ruby-compass
pkgver=1.0.1
pkgrel=2
pkgdesc="A Real Stylesheet Framework"
arch=(i686 x86_64)
url="http://compass-style.org"
license=("MIT")
depends=("ruby" "ruby-bundler")
makedepends=("rubygems")
source=("http://gems.rubyforge.org/gems/compass-${pkgver}.gem"
        "Gemfile"
        "Gemfile.lock"
        "bundler.patch")
noextract=("compass-$pkgver.gem")

package() {
	cd "$srcdir"
	local _gemdir="$(ruby -e'puts Gem.default_dir')"
	gem install --ignore-dependencies --no-user-install -i "$pkgdir$_gemdir" -n "$pkgdir"/usr/bin compass-$pkgver.gem
	rm "$pkgdir$_gemdir"/cache/compass-$pkgver.gem

	install -d -m755 "$pkgdir"/usr/share/licenses/$pkgname
	install -D "$pkgdir$_gemdir"/gems/compass-$pkgver/LICENSE.markdown \
	           "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

	cd "$pkgdir$_gemdir"/gems/compass-$pkgver
	cp "$srcdir"/Gemfile{,.lock} .
	bundle install --deployment
	patch -p1 -i "$srcdir"/bundler.patch

	rm "$pkgdir"/usr/bin/compass
	ln -s "$_gemdir"/gems/compass-$pkgver/bin/compass "$pkgdir"/usr/bin/compass
}

sha256sums=('80c40c6c5974af98200064a7deabaa1ebc9bde5ef392c5f9179f9f219735ab33'
            'e198c9c1c1c664703fe79529fc9acdc25671bcce363b1204540ae3bb323467c0'
            'ee3a3bf7a061a49e2863c50dd344c28dbb56f9c6b76968e6ee3563e7a73f3a42'
            'fbee7344a019d115caddf00667e9473a4e82f2999bdff9c04ccc4aa31b979d72')
