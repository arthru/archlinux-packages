# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Tom < tomgparchaur at gmail dot com >
# Contributor: David Manouchehri <d@32t.ca>

pkgname=dropbox
pkgver=2.4.7
pkgrel=1
pkgdesc="A free service that lets you bring your photos, docs, and videos anywhere and share them easily."
arch=("i686" "x86_64")
url="http://www.dropbox.com"
license=(custom)
depends=("dbus-glib" "gtk2" "libsm")
optdepends=(
    'ufw-extras: ufw rules for dropbox'
)
conflicts=("dropbox-experimental")
options=('!strip' '!upx')

_source_arch="x86"
[ "$CARCH" = "x86_64" ] && _source_arch="x86_64"

sha512sums=('9a9d0ac84f21a8cce80d8703d4439b0d3cfaf25bc476993bf0c6774e0622c110aa5188a3d26d02ac06a261ecd956231b7b2558994ae2758336d7f208bead78b4'
            'f480b7d8940c163a721d84db8fe7df8ec9e60d150b7119c9ec9d87f5af003c9a1fe88cdb1c6349d87ba2f23e56115d1a1f081e6e1107ce6e56b64a8efdbdc97b'
            'b3e0701afe90693b99d5e23bad6b8637bc27611a42c695d12b3b990d98bf010371b266322cd54c60ffd654ed44f56a85b1fcb51b30db991af60043dc22bf1897'
            '55b4b0c024c69787045a200f08129bdaaf450446c588b14f1044269021613c756e1422ece33c16c38b612183332dbdcdfb638859480717801285b7476fe5392b'
            'b08a50766681a55e3bf9f1721549218996dd4dbef183dce4967622a98a52fdcc47325de99794b40462692213bbe390f659cf48023b407ae4fce81997af4d46e2')
[ "$CARCH" = "x86_64" ] && sha512sums[0]='6d5dc1ad1a2bc75d6a86dd48ac3fa6923ba7fa65be106cbdf589ba5a771d2e543f798817468a46e03a3b56a944e6ee89d51ea11630330ff840a47eb49852dc0a'

source=("https://dl-web.dropbox.com/u/17/${pkgname}-lnx.${_source_arch}-${pkgver}.tar.gz"
        "dropbox.png"
        "dropbox.desktop"
        "terms.txt"
        "dropbox.service")

package() {
	install -d "$pkgdir/opt"
	cp -R "$srcdir/.dropbox-dist" "$pkgdir/opt/dropbox"

	find "$pkgdir/opt/dropbox/" -type f -exec chmod 644 {} \;
	chmod 755 "$pkgdir/opt/dropbox/dropboxd"
	chmod 755 "$pkgdir/opt/dropbox/dropbox"

	install -d "$pkgdir/usr/bin"
	ln -s "/opt/dropbox/dropboxd" "$pkgdir/usr/bin/dropboxd"

	install -Dm644 "$srcdir/dropbox.desktop" "$pkgdir/usr/share/applications/dropbox.desktop"
	install -Dm644 "$srcdir/dropbox.png" "$pkgdir/usr/share/pixmaps/dropbox.png"
	install -Dm644 "$srcdir/terms.txt" "$pkgdir/usr/share/licenses/$pkgname/terms.txt"
	install -Dm644 "$srcdir/dropbox.service" "$pkgdir/usr/lib/systemd/system/dropbox@.service"
}
