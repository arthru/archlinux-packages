# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=gitlab
pkgver=5.3.0
pkgrel=1
pkgdesc="Self hosted Git management software. (web interface)"
arch=(i686 x86_64)
url="http://gitlab.org/"
license=('MIT')
install=gitlab.install
depends=('gitlab-shell' 'ruby-bundler' 'python2')
optdepends=(
	'mysql: database backend'
	'postgresql: database backend'
)
options=('!strip')

backup=(etc/gitlab/gitlab.yml etc/gitlab/resque.yml etc/gitlab/database.yml)
source=($pkgname-$pkgver.tar.gz::https://github.com/gitlabhq/gitlabhq/archive/v$pkgver.tar.gz
        gitlab.service
        gitlab-sidekiq.service)

build() {
	cd "$srcdir/gitlabhq-$pkgver"
	
	msg "Fetching bundled gems..."
	bundle install --deployment --binstubs --no-cache --without test
# 	bundle install --no-deployment --binstubs --path=vendor/bundle --no-cache --without test
}

package() {
	local datadir=/var/lib/gitlab
	local homedir=/usr/lib/gitlab
	local wwwdir=$homedir/www
	
	cd "$srcdir"
	
	install -dm755 \
		"$pkgdir/etc/gitlab" \
		"$pkgdir$homedir" \
		"$pkgdir/usr/share/doc/$pkgname" \
		"$pkgdir/usr/share/licenses/$pkgname"
		
	cp -a gitlabhq-$pkgver "$pkgdir$homedir/www"
	cd "$pkgdir$homedir/www"
	rm -rf vendor/bundle/ruby/*/cache
	
	sed -e 's|# user: git|user: gitlab|' \
		-e "s|/home/git/repositories|$datadir/repositories|" \
		-e "s|/home/git/gitlab-satellites|$datadir/satellites|" \
		-e "s|/home/git|$homedir|" \
		config/gitlab.yml.example > "$pkgdir/etc/gitlab/gitlab.yml"
		
	sed -e 's|redis.example.com|localhost|' \
		config/resque.yml.example > "$pkgdir/etc/gitlab/resque.yml"
	
	install -Dm644 config/database.yml.mysql "$pkgdir/etc/gitlab/database.yml"
	
	for conf in gitlab resque database; do
		ln -s /etc/gitlab/$conf.yml config/$conf.yml
	done
	
	sed 's|#{Gitlab.config.gitlab_shell.repos_path}/../gitlab-shell|"#{gitlab_shell_user_home}/gitlab-shell|' \
		-i lib/tasks/gitlab/info.rake
	
	mv LICENSE "$pkgdir/usr/share/licenses/$pkgname"
	mv README.md MAINTENANCE.md CONTRIBUTING.md CHANGELOG config/*.{example,mysql,postgresql} "$pkgdir/usr/share/doc/$pkgname"

	# services
	install -Dm0755 "$srcdir/gitlab.service" "$pkgdir/usr/lib/systemd/system/gitlab.service"
	install -Dm0755 "$srcdir/gitlab-sidekiq.service" "$pkgdir/usr/lib/systemd/system/gitlab-sidekiq.service"
}

sha256sums=('d2e553b70c27db6f4d8072d6286bc3edb64d8ce4418500650d52ee89f4e021b3'
            '6800e28d8871897656785df4348780ace64133741f034cb10580541506b15597'
            '1abf8e3b2ec573a438be76fbd555c2b652d466e033a861e0144be8c0768f1cfb')
