# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgbase=php54
pkgname=('php54'
         'php54-cgi'
         'php54-fpm'
         'php54-pear'
         'php54-enchant'
         'php54-gd'
         'php54-intl'
         'php54-ldap'
         'php54-mcrypt'
         'php54-mssql'
         'php54-odbc'
         'php54-pgsql'
         'php54-pspell'
         'php54-snmp'
         'php54-sqlite'
         'php54-tidy'
         'php54-xsl')
pkgver=5.4.22
pkgrel=1
arch=('i686' 'x86_64')
license=('PHP')
url='http://www.php.net'
makedepends=('imap' 'postgresql-libs' 'libldap' 'postfix'
             'sqlite' 'unixodbc' 'net-snmp' 'libzip' 'enchant' 'file' 'freetds'
             'libmcrypt' 'tidyhtml' 'aspell' 'libltdl' 'libpng' 'libjpeg' 'icu'
             'curl' 'libxslt' 'openssl' 'bzip2' 'db' 'gmp' 'freetype2'
             'pkg-config')
source=("http://www.php.net/distributions/${pkgbase%54}-${pkgver}.tar.bz2"
        'php.ini.patch' 'php-fpm.conf.in.patch'
        'logrotate.d.php-fpm' 'php-fpm.service' 'php-fpm.tmpfiles')

prepare() {
	cd ${srcdir}/${pkgbase%54}-${pkgver}

	# adjust paths
	patch -p0 -i ${srcdir}/php.ini.patch
	patch -p0 -i ${srcdir}/php-fpm.conf.in.patch

	# workaround for pkg-config 0.28
	sed -e 's/ && test -n "$OPENSSL_INCS"//g' \
		-e 's|freetype2/freetype|freetype2|' \
		-i configure
}

build() {
	local _phpconfig="--srcdir=../${pkgbase%54}-${pkgver} \
		--prefix=/opt/php54 \
		--sysconfdir=/etc/php54 \
		--localstatedir=/var \
		--with-layout=GNU \
		--with-config-file-path=/etc/php54 \
		--with-config-file-scan-dir=/etc/php54/conf.d \
		--enable-inline-optimization \
		--disable-debug \
		--disable-rpath \
		--disable-static \
		--enable-shared \
		--mandir=/usr/share/man \
		--without-pear \
		"

	local _phpextensions="--enable-bcmath=shared \
		--enable-calendar=shared \
		--enable-dba=shared \
		--enable-exif=shared \
		--enable-ftp=shared \
		--enable-gd-native-ttf \
		--enable-intl=shared \
		--enable-json=shared \
		--enable-mbregex \
		--enable-mbstring \
		--enable-pdo \
		--enable-phar=shared \
		--enable-posix=shared \
		--enable-session \
		--enable-shmop=shared \
		--enable-soap=shared \
		--enable-sockets=shared \
		--enable-sqlite-utf8 \
		--enable-sysvmsg=shared \
		--enable-sysvsem=shared \
		--enable-sysvshm=shared \
		--enable-xml \
		--enable-zip=shared \
		--with-bz2=shared \
		--with-curl=shared \
		--with-db4=/usr \
		--with-enchant=shared,/usr \
		--with-freetype-dir=shared,/usr \
		--with-gd=shared \
		--with-gdbm=shared \
		--with-gettext=shared \
		--with-gmp=shared \
		--with-iconv=shared \
		--with-icu-dir=/usr \
		--with-imap-ssl=shared \
		--with-imap=shared \
		--with-jpeg-dir=shared,/usr \
		--with-ldap=shared \
		--with-ldap-sasl \
		--with-mcrypt=shared \
		--with-mhash \
		--with-mssql=shared \
		--with-mysql-sock=/run/mysqld/mysqld.sock \
		--with-mysql=shared,mysqlnd \
		--with-mysqli=shared,mysqlnd \
		--with-openssl=shared \
		--with-pcre-regex=/usr \
		--with-pdo-mysql=shared,mysqlnd \
		--with-pdo-odbc=shared,unixODBC,/usr \
		--with-pdo-pgsql=shared \
		--with-pdo-sqlite=shared,/usr \
		--with-pgsql=shared \
		--with-png-dir=shared,/usr \
		--with-pspell=shared \
		--with-regex=php \
		--with-snmp=shared \
		--with-sqlite3=shared,/usr \
		--with-sqlite=shared \
		--with-tidy=shared \
		--with-unixODBC=shared,/usr \
		--with-xmlrpc=shared \
		--with-xsl=shared \
		--with-zlib \
		--without-db2 \
		--without-db3 \
		"

	export EXTENSION_DIR=/opt/php54/modules
	export PEAR_INSTALLDIR=/opt/php54/pear
	export CPPFLAGS="$CPPFLAGS -O2 -I/usr/include/freetype2"

	msg2 "Building CLI"
	cd "$srcdir"
	rm -rf build-php
	mkdir build-php
	cd build-php
	ln -s ../${pkgbase%54}-${pkgver}/configure
	./configure ${_phpconfig} \
		--disable-cgi \
		--with-readline \
		--enable-pcntl \
		${_phpextensions}
	make

	msg2 "Building cgi/fcgi API"
	cd "$srcdir"
	rm -rf build-cgi
	cp -a build-php build-cgi
	cd build-cgi
	./configure ${_phpconfig} \
		--disable-cli \
		--enable-cgi \
		${_phpextensions}
	make

	msg2 "Building FPM API"
	cd "$srcdir"
	rm -rf build-fpm
	cp -a build-php build-fpm
	cd build-fpm
	./configure ${_phpconfig} \
		--disable-cli \
		--enable-fpm \
		--with-fpm-user=http \
		--with-fpm-group=http \
		${_phpextensions}
	make

	msg2 "Building PEAR"
	cd "$srcdir"
	rm -rf build-pear
	cp -a build-php build-pear
	cd build-pear
	./configure ${_phpconfig} \
		--disable-cgi \
		--with-readline \
		--enable-pcntl \
		--with-pear \
		${_phpextensions}
	make
}

# check() {
# 	cd ${srcdir}/build-php
# 	make test
# }

package_php54() {
	pkgdesc='An HTML-embedded scripting language'
	provides="php=$pkgver"
	depends=('pcre' 'libxml2' 'bzip2' 'curl')
	backup=('etc/php54/php.ini')

	cd ${srcdir}/build-php
	make -j1 INSTALL_ROOT=${pkgdir} install

	# install php.ini
	install -D -m644 ${srcdir}/${pkgbase%54}-${pkgver}/php.ini-production ${pkgdir}/etc/php54/php.ini
	install -d -m755 ${pkgdir}/etc/php54/conf.d/

	# links
	install -dm755 "$pkgdir/usr/bin"
	for bin in php phar; do
		ln -s /opt/php54/bin/$bin "$pkgdir/usr/bin/${bin}54"
	done

	# remove conflicting files
	rm -rf ${pkgdir}/usr/share/man

	# remove static modules
	rm -f ${pkgdir}/opt/php54/modules/*.a
	# remove modules provided by sub packages
	rm -f ${pkgdir}/opt/php54/modules/{enchant,gd,intl,ldap,mcrypt,mssql,odbc,pdo_odbc,pgsql,pdo_pgsql,pspell,snmp,sqlite3,pdo_sqlite,tidy,xsl}.so
}

package_php54-cgi() {
	pkgdesc='CGI and FCGI SAPI for PHP'
	provides="php-cgi=$pkgver"
	depends=('php54')

	install -D -m755 ${srcdir}/build-cgi/sapi/cgi/php-cgi ${pkgdir}/opt/php54/bin/php-cgi
}

package_php54-fpm() {
	pkgdesc='FastCGI Process Manager for PHP'
	provides="php-fpm=$pkgver"
	depends=('php54' 'systemd')
	backup=('etc/php54/php-fpm.conf')
	install='php-fpm.install'

	install -D -m755 ${srcdir}/build-fpm/sapi/fpm/php-fpm ${pkgdir}/opt/php54/bin/php-fpm
	install -D -m644 ${srcdir}/build-fpm/sapi/fpm/php-fpm.conf ${pkgdir}/etc/php54/php-fpm.conf
	install -D -m644 ${srcdir}/logrotate.d.php-fpm ${pkgdir}/etc/logrotate.d/php54-fpm
	install -d -m755 ${pkgdir}/etc/php54/fpm.d
	install -D -m644 ${srcdir}/php-fpm.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/php54-fpm.conf
	install -D -m644 ${srcdir}/php-fpm.service ${pkgdir}/usr/lib/systemd/system/php54-fpm.service
}

package_php54-pear() {
	pkgdesc='PHP Extension and Application Repository'
	provides="php-pear=$pkgver"
	depends=('php54')
	backup=('etc/php54/pear.conf')

	cd ${srcdir}/build-pear
	make -j1 install-pear INSTALL_ROOT=${pkgdir}
	local i
	while read i; do
		[ ! -e "$i" ] || rm -rf "$i"
	done < <(find ${pkgdir} -name '.*')

	# links
	install -dm755 "$pkgdir/usr/bin"
	for bin in pear pecl; do
		ln -s /opt/php54/bin/$bin "$pkgdir/usr/bin/${bin}54"
	done
}

package_php54-enchant() {
	provides="php-enchant=$pkgver"
	depends=('php54' 'enchant')
	pkgdesc='enchant module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/enchant.so ${pkgdir}/opt/php54/modules/enchant.so
}

package_php54-gd() {
	provides="php-gd=$pkgver"
	depends=('php54' 'libpng' 'libjpeg' 'freetype2')
	pkgdesc='gd module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/gd.so ${pkgdir}/opt/php54/modules/gd.so
}

package_php54-intl() {
	provides="php-intl=$pkgver"
	depends=('php54' 'icu')
	pkgdesc='intl module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/intl.so ${pkgdir}/opt/php54/modules/intl.so
}

package_php54-ldap() {
	provides="php-ldap=$pkgver"
	depends=('php54' 'libldap')
	pkgdesc='ldap module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/ldap.so ${pkgdir}/opt/php54/modules/ldap.so
}

package_php54-mcrypt() {
	provides="php-mcrypt=$pkgver"
	depends=('php54' 'libmcrypt' 'libltdl')
	pkgdesc='mcrypt module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/mcrypt.so ${pkgdir}/opt/php54/modules/mcrypt.so
}

package_php54-mssql() {
	provides="php-mssql=$pkgver"
	depends=('php54' 'freetds')
	pkgdesc='mssql module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/mssql.so ${pkgdir}/opt/php54/modules/mssql.so
}

package_php54-odbc() {
	provides="php-odbc=$pkgver"
	depends=('php54' 'unixodbc')
	pkgdesc='ODBC modules for PHP'
	install -D -m755 ${srcdir}/build-php/modules/odbc.so ${pkgdir}/opt/php54/modules/odbc.so
	install -D -m755 ${srcdir}/build-php/modules/pdo_odbc.so ${pkgdir}/opt/php54/modules/pdo_odbc.so
}

package_php54-pgsql() {
	provides="php-pgsql=$pkgver"
	depends=('php54' 'postgresql-libs')
	pkgdesc='PostgreSQL modules for PHP'
	install -D -m755 ${srcdir}/build-php/modules/pgsql.so ${pkgdir}/opt/php54/modules/pgsql.so
	install -D -m755 ${srcdir}/build-php/modules/pdo_pgsql.so ${pkgdir}/opt/php54/modules/pdo_pgsql.so
}

package_php54-pspell() {
	provides="php-pspell=$pkgver"
	depends=('php54' 'aspell')
	pkgdesc='pspell module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/pspell.so ${pkgdir}/opt/php54/modules/pspell.so
}

package_php54-snmp() {
	provides="php-snmp=$pkgver"
	depends=('php54' 'net-snmp')
	pkgdesc='snmp module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/snmp.so ${pkgdir}/opt/php54/modules/snmp.so
}

package_php54-sqlite() {
	provides="php-sqlite=$pkgver"
	depends=('php54' 'sqlite')
	pkgdesc='sqlite module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/sqlite3.so ${pkgdir}/opt/php54/modules/sqlite3.so
	install -D -m755 ${srcdir}/build-php/modules/pdo_sqlite.so ${pkgdir}/opt/php54/modules/pdo_sqlite.so
}

package_php54-tidy() {
	provides="php-tidy=$pkgver"
	depends=('php54' 'tidyhtml')
	pkgdesc='tidy module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/tidy.so ${pkgdir}/opt/php54/modules/tidy.so
}

package_php54-xsl() {
	provides="php-xsl=$pkgver"
	depends=('php54' 'libxslt')
	pkgdesc='xsl module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/xsl.so ${pkgdir}/opt/php54/modules/xsl.so
}

sha256sums=('3b8619b030e372f2b64e3a059d05a3ef3354e81f8a72923ba45475bf222f7cca'
            '754391c75a3304a6442492d4885b13b16ba2cdf0d0c7f0e605d9929a2035db9f'
            '8386e4d937ec909cadc9403a9eb50dc0aa278f02d8420eafed5734df94a7e3b7'
            '2608db714797c9ffc65dd6a7119221738b7de5abe55f105986bdd50152d6b7c8'
            '14e01c1b51be18885fe224c37d36f9408e3f05f9cb3bb482a0743345da002442'
            'ff65ed80264ff16e8ff8230d603a899947411538d96773dc098b399ab6c95919')
