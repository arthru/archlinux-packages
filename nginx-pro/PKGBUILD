_cfgdir=/etc/nginx
_tmpdir=/var/lib/nginx

pkgname=nginx-pro
pkgver=1.4.1
pkgrel=1
_naxsiver=0.49
_uploadprogver=0.9.0
_cachepurgever=2.1
pkgdesc="Lightweight HTTP server and IMAP/POP3 proxy server with some additional modules"
arch=('i686' 'x86_64')
depends=('pcre' 'zlib' 'openssl')
makedepends=('git' 'passenger')
optdepends=('passenger')
url="http://nginx.org"
license=('custom')
conflicts=('nginx')
provides=('nginx')

backup=(${_cfgdir:1}/fastcgi.conf
        ${_cfgdir:1}/fastcgi_params
        ${_cfgdir:1}/koi-win
        ${_cfgdir:1}/koi-utf
        ${_cfgdir:1}/mime.types
        ${_cfgdir:1}/nginx.conf
        ${_cfgdir:1}/scgi_params
        ${_cfgdir:1}/uwsgi_params
        ${_cfgdir:1}/win-utf
        ${_cfgdir:1}/naxsi_core.rules
        ${_cfgdir:1}/naxsi.conf
        etc/logrotate.d/nginx)

source=(http://nginx.org/download/nginx-$pkgver.tar.gz
        http://naxsi.googlecode.com/files/naxsi-core-$_naxsiver.tgz
        http://naxsi.googlecode.com/files/naxsi-ui-$_naxsiver.tgz
        nginx-upload-progress-$_uploadprogver.tgz::https://github.com/masterzen/nginx-upload-progress-module/tarball/v$_uploadprogver
        http://labs.frickle.com/files/ngx_cache_purge-$_cachepurgever.tar.gz
        service
        nginx.logrotate)

build() {
	local _modules=(
		--add-module=/usr/lib/passenger/ext/nginx
		--add-module=../naxsi-core-$_naxsiver/naxsi_src
		--add-module=../masterzen-nginx-upload-progress-module-a788dea/
		--add-module=../ngx_cache_purge-$_cachepurgever
	)

	cd "$srcdir"

	msg "Fetching nginx-push-stream-module"
	_modules=(${_modules[@]} --add-module=../nginx-push-stream-module)
	if [ -d nginx-push-stream-module ]; then
		pushd nginx-push-stream-module
		git pull
		popd
	else
		git clone --depth=0 https://github.com/wandenberg/nginx-push-stream-module.git
	fi

	msg "Fetching nginx-dav-ext-module"
	_modules=(${_modules[@]} --add-module=../nginx-dav-ext-module)
	if [ -d nginx-dav-ext-module ]; then
		pushd nginx-dav-ext-module
		git pull
		popd
	else
		git clone --depth=0 https://github.com/arut/nginx-dav-ext-module.git
	fi

	cd "$srcdir/nginx-$pkgver"

	msg "Building nginx"

	./configure \
        --prefix=$_cfgdir \
        --conf-path=$_cfgdir/nginx.conf \
        --sbin-path=/usr/bin/nginx \
        --pid-path=/var/run/nginx.pid \
        --lock-path=/var/lock/nginx.lock \
        --user=http --group=http \
        --http-log-path=/var/log/nginx/access.log \
        --error-log-path=/var/log/nginx/error.log \
        --http-client-body-temp-path=$_tmpdir/client-body \
        --http-proxy-temp-path=$_tmpdir/proxy \
        --http-fastcgi-temp-path=$_tmpdir/fastcgi \
        --http-scgi-temp-path=$_tmpdir/scgi \
        --http-uwsgi-temp-path=$_tmpdir/uwsgi \
        --with-imap --with-imap_ssl_module \
        --with-ipv6 --with-pcre-jit \
        --with-file-aio \
        --with-http_dav_module \
        --with-http_geoip_module \
        --with-http_gunzip_module \
        --with-http_gzip_static_module \
        --with-http_realip_module \
        --with-http_spdy_module \
        --with-http_ssl_module \
        --with-http_stub_status_module \
        --without-mail_pop3_module --without-mail_imap_module --without-mail_smtp_module \
        ${_modules[@]} \
        #--with-http_mp4_module \
        #--with-http_addition_module \
        #--with-http_xslt_module \
        #--with-http_image_filter_module \
        #--with-http_sub_module \
        #--with-http_flv_module \
        #--with-http_random_index_module \
        #--with-http_secure_link_module \
        #--with-http_degradation_module \
        #--with-http_perl_module \

	make
}

package() {
	cd "$srcdir"/nginx-$pkgver
	make DESTDIR="$pkgdir" install

	sed -e 's|\<user\s\+\w\+;|user html;|g' \
	    -e '2s|worker_processes  1|worker_processes auto|' \
	    -e '44s|html|/usr/share/nginx/html|' \
	    -e '54s|html|/usr/share/nginx/html|' \
	    -i "$pkgdir"$_cfgdir/nginx.conf
	rm "$pkgdir"/etc/nginx/*.default

	install -d "$pkgdir"/$_tmpdir
	install -dm700 "$pkgdir"/$_tmpdir/proxy
	
	chmod 750 "$pkgdir"/var/log/nginx
	chown http:log "$pkgdir"/var/log/nginx

	install -d "$pkgdir"/usr/share/nginx
	mv "$pkgdir"/etc/nginx/html/ "$pkgdir"/usr/share/nginx

	install -Dm644 "$srcdir"/naxsi-core-$_naxsiver/naxsi_config/default_location_config.example "$pkgdir$_cfgdir/naxsi.conf"
	install -Dm644 "$srcdir"/naxsi-core-$_naxsiver/naxsi_config/naxsi_core.rules "$pkgdir$_cfgdir/naxsi_core.rules"

	cp -a "$srcdir"/naxsi-ui-$_naxsiver/naxsi-ui "$pkgdir"/usr/share/

	install -Dm644 "$srcdir"/logrotate "$pkgdir"/etc/logrotate.d/nginx
	install -Dm644 "$srcdir"/service "$pkgdir"/usr/lib/systemd/system/nginx.service
	install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/nginx/LICENSE
	rm -rf "$pkgdir"/var/run
}

sha256sums=('bca5d1e89751ba29406185e1736c390412603a7e6b604f5b4575281f6565d119'
            'bb0b63713d5a9446401d5acd1ef0882b9c2a0fc506502bbd88bc4663d273e9c7'
            '54289f263a915b1222030bab97e4210619a893515da9ce41a1d9e95f8b87ddbe'
            '3fb903dab595cf6656fa0fc5743a48daffbba2f6b5c554836be630800eaad4e2'
            '424005af0c04e59ffa65a65e446081d4f95ee76a801a7555e001c67810bcb3b9'
            '05fdc0c0483410944b988d7f4beabb00bec4a44a41bd13ebc9b78585da7d3f9b'
            '9523a1fdd5eb61bf62f3049f6ee088b198e36d5edcce2d9b08bbeb2930aa5a16')
