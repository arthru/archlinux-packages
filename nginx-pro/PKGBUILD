_cfgdir=/etc/nginx
_tmpdir=/var/lib/nginx

pkgname=nginx-pro
pkgver=1.4.6
pkgrel=1

_uploadprogver=0.9.0
_cachepurgever=2.1
_davextver=0.0.3
_pushstreamver=0.4.0
_modver=2.7.7

pkgdesc="Lightweight HTTP server and IMAP/POP3 proxy server with some additional modules"
arch=('i686' 'x86_64')
depends=('pcre' 'zlib' 'openssl' 'apr-util' 'geoip')
makedepends=('git')
url="http://nginx.org"
license=('custom')
conflicts=('nginx')
provides=('nginx')

backup=(${_cfgdir:1}/modsec-recommended.conf
        ${_cfgdir:1}/fastcgi.conf
        ${_cfgdir:1}/fastcgi_params
        ${_cfgdir:1}/koi-win
        ${_cfgdir:1}/koi-utf
        ${_cfgdir:1}/mime.types
        ${_cfgdir:1}/nginx.conf
        ${_cfgdir:1}/scgi_params
        ${_cfgdir:1}/uwsgi_params
        ${_cfgdir:1}/win-utf
        etc/logrotate.d/nginx)

source=(http://nginx.org/download/nginx-$pkgver.tar.gz
        https://github.com/SpiderLabs/ModSecurity/archive/v$_modver.tar.gz
        nginx-upload-progress-$_uploadprogver.tgz::https://github.com/masterzen/nginx-upload-progress-module/tarball/v$_uploadprogver
        nginx-dav-ext-module-$_davextver.tgz::https://github.com/arut/nginx-dav-ext-module/tarball/v$_davextver
        http://labs.frickle.com/files/ngx_cache_purge-$_cachepurgever.tar.gz
        nginx-push-stream-module-$_pushstreamver.tgz::https://github.com/wandenberg/nginx-push-stream-module/tarball/$_pushstreamver
        http://labs.frickle.com/files/ngx_cache_purge-$_cachepurgever.tar.gz
        modsec-recommended.conf
        service
        nginx.logrotate)

build() {
	local _modules=(
		--add-module=../masterzen-nginx-upload-progress-module-a788dea/
		--add-module=../ngx_cache_purge-$_cachepurgever
		--add-module=../arut-nginx-dav-ext-module-6974186
		--add-module=../wandenberg-nginx-push-stream-module-3d3a204
	)

	msg "Compiling mod-security"
	_modules=(${_modules[@]} --add-module=../ModSecurity-$_modver/nginx/modsecurity)
	cd "$srcdir/ModSecurity-$_modver"
	sh autogen.sh
 	CFLAGS="-g -O0" ./configure \
		--prefix=/usr \
		--enable-standalone-module \
		--disable-apache2-module \
		--enable-pcre-study \
		--enable-pcre-jit \
		--enable-pcre-match-limit=1000 \
		--enable-pcre-match-limit-recursion=1000 \
		--enable-lua-cache
	sed -r '/\$\$base/d' -i standalone/Makefile
 	make
 	make DESTDIR="$srcdir/ModSecurity-$_modver/install" install

	cd "$srcdir/nginx-$pkgver"

	msg "Building nginx"

	CFLAGS="-I /usr/include/httpd -I /usr/include/apr-1" ./configure \
        --prefix=$_cfgdir \
        --conf-path=$_cfgdir/nginx.conf \
        --sbin-path=/usr/bin/nginx \
        --pid-path=/var/run/nginx.pid \
        --lock-path=/var/lock/nginx.lock \
        --user=http --group=http \
        --http-log-path=/var/log/nginx/access.log \
        --error-log-path=/var/log/nginx/error.log \
        --http-client-body-temp-path=$_tmpdir/client-body \
        --http-proxy-temp-path=$_tmpdir/proxy \
        --http-fastcgi-temp-path=$_tmpdir/fastcgi \
        --http-scgi-temp-path=$_tmpdir/scgi \
        --http-uwsgi-temp-path=$_tmpdir/uwsgi \
        --with-imap --with-imap_ssl_module \
        --with-ipv6 --with-pcre-jit \
        --with-file-aio \
        --with-http_dav_module \
        --with-http_geoip_module \
        --with-http_gunzip_module \
        --with-http_gzip_static_module \
        --with-http_realip_module \
        --with-http_spdy_module \
        --with-http_ssl_module \
        --with-http_stub_status_module \
        --without-mail_pop3_module --without-mail_imap_module --without-mail_smtp_module \
        ${_modules[@]} \
        #--with-http_mp4_module \
        #--with-http_addition_module \
        #--with-http_xslt_module \
        #--with-http_image_filter_module \
        #--with-http_sub_module \
        #--with-http_flv_module \
        #--with-http_random_index_module \
        #--with-http_secure_link_module \
        #--with-http_degradation_module \
        #--with-http_perl_module \

	make
}

package() {
	cd "$srcdir"/nginx-$pkgver
	make DESTDIR="$pkgdir" install

	for bin in "$srcdir/ModSecurity-$_modver"/install/usr/bin/*; do
		binname=$(basename "$bin")
		install -Dm755 "$bin" "$pkgdir/usr/bin/$binname"
	done

	sed -e 's|\<user\s\+\w\+;|user html;|g' \
	    -e '2s|worker_processes  1|worker_processes auto|' \
	    -e '44s|html|/usr/share/nginx/html|' \
	    -e '54s|html|/usr/share/nginx/html|' \
	    -i "$pkgdir"$_cfgdir/nginx.conf
	rm "$pkgdir"/etc/nginx/*.default

	install -Dm644 "$srcdir/modsec-recommended.conf" "$pkgdir/$_cfgdir/modsec-recommended.conf"

	install -d "$pkgdir"/$_tmpdir
	install -dm700 -o http "$pkgdir"/$_tmpdir/{proxy,modsecurity/{suspicious,msa}}
	
	install -d "$pkgdir"/var/log
	install -dm750 -o http -g log "$pkgdir"/var/log/nginx/modsec

	install -d "$pkgdir"/usr/share/nginx
	mv "$pkgdir"/etc/nginx/html/ "$pkgdir"/usr/share/nginx

	install -Dm644 "$srcdir"/nginx.logrotate "$pkgdir"/etc/logrotate.d/nginx
	install -Dm644 "$srcdir"/service "$pkgdir"/usr/lib/systemd/system/nginx.service
	install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/nginx/LICENSE
	rm -rf "$pkgdir"/var/run
}

sha256sums=('7a8b5b15d708b5b9c61e723bd93faa247b06c8b90babb76f612c128edb5812c6'
            'fe9b599490c9667c72b215de263a913564ea1933b9dbeff8b99c9144aa65be26'
            '3fb903dab595cf6656fa0fc5743a48daffbba2f6b5c554836be630800eaad4e2'
            '6f40a158be9f60918162cd1cdf4ab01c7ad2d07c94983e44240ce4201a4f07a0'
            '424005af0c04e59ffa65a65e446081d4f95ee76a801a7555e001c67810bcb3b9'
            '2712faf70bae3008f38e3bb2b0096b1274d11ac861f2c29c0f2917f0a519e4af'
            '424005af0c04e59ffa65a65e446081d4f95ee76a801a7555e001c67810bcb3b9'
            '838637cc4f5fd9d3209720db23234d99972e95549d8ac8eb619474f66376e8b1'
            '05fdc0c0483410944b988d7f4beabb00bec4a44a41bd13ebc9b78585da7d3f9b'
            '9523a1fdd5eb61bf62f3049f6ee088b198e36d5edcce2d9b08bbeb2930aa5a16')
