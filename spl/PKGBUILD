# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgbase=spl
pkgname=('spl'
         'spl-dkms')
pkgver=0.6.2
pkgrel=1
url="http://zfsonlinux.org"
arch=("x86_64" "i686")
license=("CDDL")

source=("http://archive.zfsonlinux.org/downloads/zfsonlinux/$pkgbase/$pkgbase-$pkgver.tar.gz")

prepare() {
    cd "$srcdir"
    rm -rf build
    cp -a $pkgbase-$pkgver build
}

build() {
    cd "$srcdir/$pkgbase-$pkgver"
    ./scripts/dkms.mkconf -n $pkgbase -v $pkgver -f dkms.conf

    cd "$srcdir/build"
    ./autogen.sh
    ./configure --prefix=/usr \
                --sbindir=/usr/bin \
                --libdir=/usr/lib \
                --with-config=user
    make
}

package_spl() {
    pkgdesc="OpenSolaris emulation layer utils"
    depends=("$pkgbase-dkms")

    cd "$srcdir/build"
    make DESTDIR="$pkgdir" install
}

package_spl-dkms() {
    pkgdesc="OpenSolaris primitives emulation kernel modules (DKMS)"
    install=$pkgbase-dkms.install

    install -dm755 "$pkgdir/usr/src"
    cp -a "$srcdir/$pkgbase-$pkgver" "$pkgdir/usr/src"
}

sha256sums=('3c577c7055d6c73179726b9c8a7fd48f9122be0b345c50cd54732e801165daa4')
