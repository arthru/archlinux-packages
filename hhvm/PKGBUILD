# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: James Miller <james@pocketrent.com>

pkgname=hhvm
pkgver=2.4.0
pkgrel=1
pkgdesc="Virtual Machine, Runtime, and JIT for PHP"
arch=('x86_64')
url="http://hhvm.com"
license=('PHP')
depends=('boost-libs' 'google-glog' 'libmysqlclient' 'libmemcached' 'pcre' 'gd'
         'libxml2' 'intel-tbb' 'libmcrypt' 'oniguruma' 'jemalloc' 'curl' 'imap'
         'libdwarf' 'libunwind')
makedepends=('git' 'cmake' 'gcc' 'chrpath' 'boost')
source=("$pkgname-$pkgver.tar.gz::https://github.com/facebook/hhvm/archive/HHVM-$pkgver.tar.gz"
        'https://github.com/facebook/hhvm/pull/1600.diff'
        'git+https://github.com/libevent/libevent.git#tag=release-1.4.14b-stable'
        'git+https://github.com/facebook/folly.git'
        'hhvm.tmpfile'
        'hhvm@.service'
        'static.mime-types.hdf'
        'default.hdf'
        'server.hdf')
install=hhvm.install
backup=(etc/hhvm/{conf.d/default,conf.d/static.mime-types,server}.hdf)

prepare() {
    cd "$srcdir/$pkgname-HHVM-$pkgver"

    # pull request 1600 (reflection fixes)
    patch -p1 -i "$srcdir/1600.diff"

    cd hphp/submodules
    rm -rf folly
    ln -s "$srcdir/folly"

    cd "$srcdir/libevent"
    patch -p1 -i "$srcdir/$pkgname-HHVM-$pkgver/hphp/third_party/libevent-1.4.14.fb-changes.diff"

    # replace hashbang 'python' with 'python2'
    sed '1 s/\bpython\b/python2/' -i event_rpcgen.py

    # git fix
    cd "$srcdir"
    mkdir -p bin
    [ ! -f bin/git ] && ln -s /usr/bin/false bin/git || true
}

build() {
    cd "$srcdir/libevent"
    msg2 "Building Custom Libevent"

    ./autogen.sh
    ./configure --prefix="$srcdir" --disable-shared
    make
    make install

    cd "$srcdir/$pkgname-HHVM-$pkgver"
    msg2 "Building hhvm"

    export PATH="$srcdir/bin:$PATH" # git fix
    export HPHP_HOME="$PWD"

    # comment for tests
    HPHP_NOTEST=1 \
    INCLUDE=/usr/include/libdwarf \
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_PREFIX_PATH="$srcdir" \
        .

    make hhvm
}

# check() {
#     cd "$srcdir/$pkgname-HHVM-$pkgver/hphp/test"
#     ./run --threads 8 quick
# }

package() {
    cd "$srcdir/$pkgname-HHVM-$pkgver"
    make DESTDIR="$pkgdir/" install
    rm -rf "$pkgdir"/usr/{lib,include}

    cd "$srcdir"
    install -Dm644 hhvm.tmpfile "$pkgdir"/usr/lib/tmpfiles.d/hhvm.conf
    install -Dm644 hhvm@.service "$pkgdir"/usr/lib/systemd/system/hhvm@.service

    install -Dm644 static.mime-types.hdf "$pkgdir"/etc/hhvm/conf.d/static.mime-types.hdf
    install -Dm644 default.hdf "$pkgdir"/etc/hhvm/conf.d/default.hdf
    install -Dm644 server.hdf "$pkgdir"/etc/hhvm/server.hdf

    install -dm755 -o http "$pkgdir"/var/log/hhvm
}

sha256sums=('d88af8655a74c699d77a9b04aa8e94ea5ee88748fb82089bbf623e35a8200f46'
            'b5c091a2849d164dbfd0d0d106f109998657c2f9102a9dc08b7ce3161e765055'
            'SKIP'
            'SKIP'
            'cabf505fa85e33d7149f90fae95c6f67d5148895fc6efa353b21a3b31c1c3ec6'
            '8681582439385807fd0f8caff780953d3049de93218ac785c48f6df4f4296bce'
            '8b9f1dcb0a2fc7d3657e70b880425eccb963e0566247d99d26513d141e84a937'
            '6d48e6bdc546d60a9770d507366ad23702f9997613cbe355d76463e0bf052f68'
            'ba82950161ff38e634841360d7e15cf5314f233ed4d97b02c18e0ba9f7a3f560')
