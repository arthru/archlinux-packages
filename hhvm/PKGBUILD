# Maintainer: James Miller <james@pocketrent.com>

pkgname=hhvm
pkgver=2.3.1
pkgrel=1
pkgdesc="Virtual Machine, Runtime, and JIT for PHP"
arch=('x86_64')
url="http://hhvm.com"
license=('PHP')
depends=('boost-libs' 'google-glog' 'libmysqlclient' 'libmemcached' 'pcre' 'gd'
         'libxml2' 'intel-tbb' 'libmcrypt' 'oniguruma' 'libldap' 'elfutils'
         'jemalloc' 'curl' 'imap' 'libdwarf')
makedepends=('git' 'cmake' 'gcc' 'chrpath' 'boost')
source=("$pkgname-$pkgver.tar.gz::https://github.com/facebook/hhvm/archive/HHVM-$pkgver.tar.gz"
        'git+https://github.com/libevent/libevent.git#tag=release-1.4.14b-stable'
        'git+https://github.com/facebook/folly.git#commit=8e8b5e75d573305b7a9c34f4a405be5df0f17738'
        'hhvm.tmpfile'
        'hhvm-fcgi.service'
        'static.mime-types.hdf'
        'config.hdf'
        'fastcgi.hdf')
install=hhvm.install
backup=(etc/hhvm/{config,fastcgi}.hdf)

prepare() {
	cd "$srcdir/$pkgname-HHVM-$pkgver"

	cd hphp/submodules
	rm -rf folly
	ln -s "$srcdir/folly"

	cd "$srcdir/libevent"
	patch -p1 -i "$srcdir/$pkgname-HHVM-$pkgver/hphp/third_party/libevent-1.4.14.fb-changes.diff"

	# replace hashbang 'python' with 'python2'
	sed '1 s/\bpython\b/python2/' -i event_rpcgen.py
}

build() {
    export CMAKE_PREFIX_PATH="$srcdir"

	cd "$srcdir/libevent"
	msg2 "Building Custom Libevent"
	./autogen.sh
	./configure --prefix=$CMAKE_PREFIX_PATH --disable-shared
	make

	cd "$srcdir/$pkgname-HHVM-$pkgver"

    LDFLAGS="$LDFLAGS -lgssapi_krb5 -lkrb5 -lk5crypto -lcom_err" \
	CC=gcc \
    CXX=g++ \
    INCLUDE=/usr/include/libdwarf \
    HPHP_NOTEST=1 \
    HPHP_HOME="$srcdir/$pkgname-HHVM-$pkgver" \
	cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        .

	make hhvm
}

package() {
	cd "$srcdir/$pkgname-HHVM-$pkgver"
	make DESTDIR="$pkgdir/" install
    rm -rf "$pkgdir"/usr/{lib,include}

    cd "$srcdir"
    install -Dm644 hhvm.tmpfile "$pkgdir"/usr/lib/tmpfiles.d/hhvm.conf
    install -Dm644 hhvm-fcgi.service "$pkgdir"/usr/lib/systemd/system/hhvm-fcgi.service
    install -Dm644 static.mime-types.hdf "$pkgdir"/usr/share/hhvm/hdf/static.mime-types.hdf

    install -Dm644 config.hdf "$pkgdir"/etc/hhvm/config.hdf
    install -Dm644 fastcgi.hdf "$pkgdir"/etc/hhvm/fastcgi.hdf

    install -dm755 -o http "$pkgdir"/var/log/hhvm
}

sha256sums=('2c5bdfff63e320024f312ed30bd24a9ed6a280c7d6cb98e4816fb9731787df69'
            'SKIP'
            'SKIP'
            'cabf505fa85e33d7149f90fae95c6f67d5148895fc6efa353b21a3b31c1c3ec6'
            '0389cdf822577327223588a4015bc27ed8cf81c63019c37918463f01532d3340'
            '8b9f1dcb0a2fc7d3657e70b880425eccb963e0566247d99d26513d141e84a937'
            '29a7b9acb9ee22c0382a5b0ee6723c9fab671f00b15ea08879aca269989b0e83'
            '09f689acfdf72b086f35e7a5a96fd99b3815237a2d188b5b7aa60eeafd6aa72b')
