# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: James Miller <james@pocketrent.com>

pkgname=hhvm
pkgver=3.1.0
_folly_commit=b215baa25240dc038c53c6b9b333dc040ada8d59
_thirdparty_commit=5baba1bca65cb10531fa959bd997b3715698902d
pkgrel=1
pkgdesc="Virtual Machine, Runtime, and JIT for PHP"
arch=('x86_64')
url="http://hhvm.com"
license=('PHP')
depends=('boost-libs' 'google-glog' 'libmysqlclient' 'libmemcached' 'pcre' 'gd'
         'libxml2' 'libxslt' 'intel-tbb' 'libmcrypt' 'oniguruma' 'jemalloc' 'curl' 'imap'
         'libdwarf' 'libunwind' 'imagemagick' 'libedit')
makedepends=('git' 'cmake' 'gcc' 'chrpath' 'boost' 'double-conversion' 'google-gflags' 'python2')
source=("https://github.com/facebook/hhvm/archive/HHVM-$pkgver.tar.gz"
        'git+https://github.com/libevent/libevent.git#tag=release-1.4.14b-stable'
        "folly-$_folly_commit.tar.gz::https://github.com/facebook/folly/archive/$_folly_commit.tar.gz"
        "hhvm-third-party-$_thirdparty_commit.tar.gz::https://github.com/hhvm/hhvm-third-party/archive/$_thirdparty_commit.tar.gz"
        'hhvm.tmpfile'
        'hhvm@.service'
        'static.mime-types.hdf'
        'default.hdf'
        'server.hdf')
install=hhvm.install
backup=(etc/hhvm/{conf.d/default,conf.d/static.mime-types,server}.hdf)

prepare() {
    cd "$srcdir"/$pkgname-HHVM-$pkgver

    # fix freetype includes path
    sed 's|#include <freetype/config/ftheader.h>|#include <freetype2/config/ftheader.h>|' \
        -i hphp/runtime/ext/gd/libgd/gdft.cpp

    rm -rf third-party
    ln -s "$srcdir"/hhvm-third-party-$_thirdparty_commit third-party

    cd third-party/folly
    rm -rf src
    ln -s "$srcdir"/folly-$_folly_commit src

    cd "$srcdir/libevent"
    patch -p1 -i "$srcdir"/$pkgname-HHVM-$pkgver/third-party/libevent-1.4.14.fb-changes.diff

    # replace hashbang 'python' with 'python2'
    sed '1 s/\bpython\b/python2/' -i event_rpcgen.py

    # git fix
    cd "$srcdir"
    mkdir -p bin
    [ ! -f bin/git ] && ln -s /usr/bin/false bin/git || true
}

build() {
    cd "$srcdir/libevent"
    msg2 "Building Custom Libevent"

    ./autogen.sh
    ./configure --prefix="$srcdir" --disable-shared
    make
    make install

    cd "$srcdir"/$pkgname-HHVM-$pkgver
    msg2 "Building hhvm"

    export PATH="$srcdir/bin:$PATH" # git fix
    export HPHP_HOME="$PWD"

    # comment for tests
    HPHP_NOTEST=1 \
    INCLUDE=/usr/include/libdwarf \
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_PREFIX_PATH="$srcdir" \
        -DFREETYPE_INCLUDE_DIRS:PATH=/usr/include/freetype2 \
        -DLIBDWARF_HAVE_ENCODE_LEB128:INTERNAL=1 \
        .

    make

    # needs pfff
    # for hacktool in hackificator remove_soft_types; do
    #     cd "$srcdir"/$pkgname-HHVM-$pkgver/hphp/hack/tools/$hacktool
    #     make depend
    #     make
    # done
}

# check() {
#     cd "$srcdir"/$pkgname-HHVM-$pkgver/hphp/test
#     ./run --threads 8 quick
# }

package() {
    cd "$srcdir"/$pkgname-HHVM-$pkgver
    make DESTDIR="$pkgdir/" install
    rm -rf "$pkgdir"/usr/{lib,include}

    # cd hphp/hack/bin
    # for bin in hh_*; do
    #     install -Dm755 $bin "$pkgdir"/usr/bin/$bin
    # done

    cd "$srcdir"
    install -Dm644 hhvm.tmpfile "$pkgdir"/usr/lib/tmpfiles.d/hhvm.conf
    install -Dm644 hhvm@.service "$pkgdir"/usr/lib/systemd/system/hhvm@.service

    install -Dm644 static.mime-types.hdf "$pkgdir"/etc/hhvm/conf.d/static.mime-types.hdf
    install -Dm644 default.hdf "$pkgdir"/etc/hhvm/conf.d/default.hdf
    install -Dm644 server.hdf "$pkgdir"/etc/hhvm/server.hdf

    install -dm755 -o http "$pkgdir"/var/log/hhvm
}

sha256sums=('9bcf0dc69af3d5cf64d57915912e92a76b360e4ae95762953de059d13db04c4d'
            'SKIP'
            'cd2fa3a9348ffc27a4a289c32867ae131aacf1801427d68e52d241ebea6b5fdc'
            '8e037251d41c613eb465852ab2ecef8eb7fe37c1395eedc69000132b343c7fbe'
            'cabf505fa85e33d7149f90fae95c6f67d5148895fc6efa353b21a3b31c1c3ec6'
            '8681582439385807fd0f8caff780953d3049de93218ac785c48f6df4f4296bce'
            '8b9f1dcb0a2fc7d3657e70b880425eccb963e0566247d99d26513d141e84a937'
            '6d48e6bdc546d60a9770d507366ad23702f9997613cbe355d76463e0bf052f68'
            'ba82950161ff38e634841360d7e15cf5314f233ed4d97b02c18e0ba9f7a3f560')
