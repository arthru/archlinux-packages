# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: James Miller <james@pocketrent.com>

pkgname=hhvm
pkgver=3.0.1
_folly_commit=d9c79af86a4dc0576d8df453804d3354347c07dc
pkgrel=2
pkgdesc="Virtual Machine, Runtime, and JIT for PHP"
arch=('x86_64')
url="http://hhvm.com"
license=('PHP')
depends=('boost-libs' 'google-glog' 'libmysqlclient' 'libmemcached' 'pcre' 'gd'
         'libxml2' 'libxslt' 'intel-tbb' 'libmcrypt' 'oniguruma' 'jemalloc' 'curl' 'imap'
         'libdwarf' 'libunwind' 'imagemagick' 'libedit')
makedepends=('git' 'cmake' 'gcc' 'chrpath' 'boost' 'double-conversion' 'google-gflags' 'python2')
source=("https://github.com/facebook/hhvm/archive/HHVM-$pkgver.tar.gz"
        'git+https://github.com/libevent/libevent.git#tag=release-1.4.14b-stable'
        "folly-$_folly_commit.tar.gz::https://github.com/facebook/folly/archive/$_folly_commit.tar.gz"
        'hhvm.tmpfile'
        'hhvm@.service'
        'static.mime-types.hdf'
        'default.hdf'
        'server.hdf')
install=hhvm.install
backup=(etc/hhvm/{conf.d/default,conf.d/static.mime-types,server}.hdf)

prepare() {
    cd "$srcdir"/$pkgname-HHVM-$pkgver

    # fix freetype includes path
    sed 's|#include <freetype/config/ftheader.h>|#include <freetype2/config/ftheader.h>|' \
        -i hphp/runtime/ext/gd/libgd/gdft.cpp

    cd hphp/submodules
    rm -rf folly
    ln -s "$srcdir"/folly-$_folly_commit folly

    cd "$srcdir/libevent"
    patch -p1 -i "$srcdir"/$pkgname-HHVM-$pkgver/hphp/third_party/libevent-1.4.14.fb-changes.diff

    # replace hashbang 'python' with 'python2'
    sed '1 s/\bpython\b/python2/' -i event_rpcgen.py

    # git fix
    cd "$srcdir"
    mkdir -p bin
    [ ! -f bin/git ] && ln -s /usr/bin/false bin/git || true
}

build() {
    cd "$srcdir/libevent"
    msg2 "Building Custom Libevent"

    ./autogen.sh
    ./configure --prefix="$srcdir" --disable-shared
    make
    make install

    cd "$srcdir"/$pkgname-HHVM-$pkgver
    msg2 "Building hhvm"

    export PATH="$srcdir/bin:$PATH" # git fix
    export HPHP_HOME="$PWD"

    # comment for tests
    HPHP_NOTEST=1 \
    INCLUDE=/usr/include/libdwarf \
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_PREFIX_PATH="$srcdir" \
        -DFREETYPE_INCLUDE_DIRS:PATH=/usr/include/freetype2 \
        -DLIBDWARF_HAVE_ENCODE_LEB128:INTERNAL=1 \
        .

    make

    # needs pfff
    # for hacktool in hackificator remove_soft_types; do
    #     cd "$srcdir"/$pkgname-HHVM-$pkgver/hphp/hack/tools/$hacktool
    #     make depend
    #     make
    # done
}

# check() {
#     cd "$srcdir"/$pkgname-HHVM-$pkgver/hphp/test
#     ./run --threads 8 quick
# }

package() {
    cd "$srcdir"/$pkgname-HHVM-$pkgver
    make DESTDIR="$pkgdir/" install
    rm -rf "$pkgdir"/usr/{lib,include}

    # cd hphp/hack/bin
    # for bin in hh_*; do
    #     install -Dm755 $bin "$pkgdir"/usr/bin/$bin
    # done

    cd "$srcdir"
    install -Dm644 hhvm.tmpfile "$pkgdir"/usr/lib/tmpfiles.d/hhvm.conf
    install -Dm644 hhvm@.service "$pkgdir"/usr/lib/systemd/system/hhvm@.service

    install -Dm644 static.mime-types.hdf "$pkgdir"/etc/hhvm/conf.d/static.mime-types.hdf
    install -Dm644 default.hdf "$pkgdir"/etc/hhvm/conf.d/default.hdf
    install -Dm644 server.hdf "$pkgdir"/etc/hhvm/server.hdf

    install -dm755 -o http "$pkgdir"/var/log/hhvm
}

sha256sums=('ff58b79ab815750516403fe444be78d58c2840a3712dd224e4109ee9b6e4e370'
            'SKIP'
            '0db0f79c45411b3a90889fecd1fb0b505abe51a6fe24724e62372bd4e52abf49'
            'cabf505fa85e33d7149f90fae95c6f67d5148895fc6efa353b21a3b31c1c3ec6'
            '8681582439385807fd0f8caff780953d3049de93218ac785c48f6df4f4296bce'
            '8b9f1dcb0a2fc7d3657e70b880425eccb963e0566247d99d26513d141e84a937'
            '6d48e6bdc546d60a9770d507366ad23702f9997613cbe355d76463e0bf052f68'
            'ba82950161ff38e634841360d7e15cf5314f233ed4d97b02c18e0ba9f7a3f560')
