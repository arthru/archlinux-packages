# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: James Miller <james@pocketrent.com>

pkgname=hhvm
pkgver=3.2.0
_folly_commit=09a81a96ea2f9790242675f3c84013266c38d684
_thirdparty_commit=1b7752522776202e7051031004cd12d97302872f
pkgrel=1
pkgdesc="Virtual Machine, Runtime, and JIT for PHP"
arch=('x86_64')
url="http://hhvm.com"
license=('PHP')
depends=('boost-libs' 'google-glog' 'libmysqlclient' 'libmemcached' 'pcre' 'gd'
         'libxml2' 'libxslt' 'intel-tbb' 'libmcrypt' 'oniguruma' 'jemalloc' 'curl' 'imap'
         'libdwarf-20140413' 'libunwind' 'imagemagick' 'libedit' 'pfff')
makedepends=('git' 'cmake' 'gcc' 'chrpath' 'boost' 'google-gflags' 'python2')
source=("https://github.com/facebook/hhvm/archive/HHVM-$pkgver.tar.gz"
        "folly-$_folly_commit.tar.gz::https://github.com/facebook/folly/archive/$_folly_commit.tar.gz"
        "hhvm-third-party-$_thirdparty_commit.tar.gz::https://github.com/hhvm/hhvm-third-party/archive/$_thirdparty_commit.tar.gz"
        "cmakelibs-1.patch::https://github.com/facebook/hhvm/commit/f420f7ea181daaefbea31be0b028ede3a92ced7a.patch"
        "cmakelibs-2.patch::https://github.com/facebook/hhvm/commit/fdcf1ba00859baca6526bfb11aca4aecc1a1c923.patch"
        'hhvm.tmpfile'
        'hhvm@.service'
        'static.mime-types.hdf'
        'default.hdf'
        'server.hdf')
install=hhvm.install
backup=(etc/hhvm/{conf.d/default,conf.d/static.mime-types,server}.hdf)

prepare() {
    cd "$srcdir"/$pkgname-HHVM-$pkgver
    patch -p1 -i "$srcdir"/cmakelibs-1.patch
    patch -p1 -i "$srcdir"/cmakelibs-2.patch

    # fix freetype includes path
    sed 's|#include <freetype/config/ftheader.h>|#include <freetype2/config/ftheader.h>|' \
        -i hphp/runtime/ext/gd/libgd/gdft.cpp

    rm -rf third-party
    ln -s "$srcdir"/hhvm-third-party-$_thirdparty_commit third-party

    cd third-party/folly
    rm -rf src
    ln -s "$srcdir"/folly-$_folly_commit src
}

build() {
    cd "$srcdir"/$pkgname-HHVM-$pkgver
    msg2 "Building hhvm"

    export PATH="$srcdir/bin:$PATH" # git fix
    export HPHP_HOME="$PWD"

    # comment for tests
    HPHP_NOTEST=1 \
    INCLUDE=/usr/include/libdwarf \
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_PREFIX_PATH="$srcdir" \
        -DFREETYPE_INCLUDE_DIRS:PATH=/usr/include/freetype2 \
        .

    make

    for hacktool in hackificator remove_soft_types; do
        cd "$srcdir"/$pkgname-HHVM-$pkgver/hphp/hack/tools/$hacktool
        make depend
        make
    done
}

# check() {
#     cd "$srcdir"/$pkgname-HHVM-$pkgver/hphp/test
#     ./run --threads 8 quick
# }

package() {
    cd "$srcdir"/$pkgname-HHVM-$pkgver
    make DESTDIR="$pkgdir/" install
    rm -rf "$pkgdir"/usr/{lib,include}

    cd hphp/hack/bin
    for bin in hh_*; do
        install -Dm755 $bin "$pkgdir"/usr/bin/$bin
    done

    cd "$srcdir"
    install -Dm644 hhvm.tmpfile "$pkgdir"/usr/lib/tmpfiles.d/hhvm.conf
    install -Dm644 hhvm@.service "$pkgdir"/usr/lib/systemd/system/hhvm@.service

    install -Dm644 static.mime-types.hdf "$pkgdir"/etc/hhvm/conf.d/static.mime-types.hdf
    install -Dm644 default.hdf "$pkgdir"/etc/hhvm/conf.d/default.hdf
    install -Dm644 server.hdf "$pkgdir"/etc/hhvm/server.hdf

    install -dm755 -o http "$pkgdir"/var/log/hhvm
}

sha256sums=('de446142d3e5470ab520a5103435aba457e1a43c8977a1b935670938659fb5fd'
            '6d18f59a3d8d0e3682bc65287342801e48e6cfdd04507b897a921acd580e7e76'
            '53e3a40fb79d4d2844e93a83f71c93fd52feac78a2d3bd27201add4f556b14b6'
            'c9b5021518079a08de70037b8dea8c7069f8cdce9f331f77fd8116373172f77e'
            '1eb7ebce65ada883daccfca6ea2416e17d93e91f5f55e1fe51bf512e3bebcd1b'
            'cabf505fa85e33d7149f90fae95c6f67d5148895fc6efa353b21a3b31c1c3ec6'
            '8681582439385807fd0f8caff780953d3049de93218ac785c48f6df4f4296bce'
            '8b9f1dcb0a2fc7d3657e70b880425eccb963e0566247d99d26513d141e84a937'
            '6d48e6bdc546d60a9770d507366ad23702f9997613cbe355d76463e0bf052f68'
            'ba82950161ff38e634841360d7e15cf5314f233ed4d97b02c18e0ba9f7a3f560')
