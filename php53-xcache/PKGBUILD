# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=php53-xcache
pkgver=3.1.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A PHP opcode cacher'
url='http://xcache.lighttpd.net/'
depends=('php53')
license=('custom')
source=("http://xcache.lighttpd.net/pub/Releases/${pkgver}/xcache-${pkgver}.tar.bz2"
        'xcache.ini')
backup=('etc/php53/conf.d/xcache.ini')

build() {
	cd "$srcdir/xcache-$pkgver"
	/opt/php53/bin/phpize
	./configure --with-php-config=/opt/php53/bin/php-config
	make
}

package() {
	cd "$srcdir/xcache-$pkgver"
	make INSTALL_ROOT="$pkgdir" install

	install -Dm644 "$srcdir/xcache.ini" "$pkgdir/etc/php53/conf.d/xcache.ini"

	install -dm755 "$pkgdir/usr/share/$pkgname/admin"
	cp -r htdocs/* "$pkgdir/usr/share/$pkgname/admin/"

	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

sha256sums=('86af3e08a8983648d9755f5e1d0e32ad5b952d3e92a83fcaf3acbac9ffd65ee9'
            'c8c843335028b1da1965ff71b73b42fc4bc45be08bf9ec3f71aff5744a428176')
