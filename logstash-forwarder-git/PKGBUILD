# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=logstash-forwarder-git
pkgver=v0.3.1.15.g8c05066
pkgrel=1
pkgdesc="Logstash shipping tool using the lumberjack protocol"
arch=('x86_64')
url="https://github.com/elasticsearch/logstash-forwarder"
license=('APACHE')
depends=('glibc')
makedepends=('go')
backup=(etc/logstash-forwarder.conf)
source=('git+https://github.com/elasticsearch/logstash-forwarder.git' 'logstash-forwarder.service')

pkgver() {
	cd "$srcdir/logstash-forwarder"
	git describe --tags --always | sed 's|-|.|g'
}

build() {
    cd "$srcdir/logstash-forwarder"
	make PREFIX=/usr
}

package() {
    cd "$srcdir/logstash-forwarder"
    install -dm755 "$pkgdir"/var/lib/logstash-forwarder
    install -Dm644 logstash-forwarder.conf.example "$pkgdir"/etc/logstash-forwarder.conf
    install -Dm755 build/bin/logstash-forwarder "$pkgdir"/usr/bin/logstash-forwarder
    install -Dm644 "$srcdir"/logstash-forwarder.service "$pkgdir"/usr/lib/systemd/system/logstash-forwarder.service
}

sha256sums=('SKIP'
            'c2346140c185d21a1216f834395340fb9c5024c604d40e19666502e7dafde836')
