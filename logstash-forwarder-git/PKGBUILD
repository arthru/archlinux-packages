# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=logstash-forwarder-git
pkgver=v0.3.1.15.g8c05066
pkgrel=2
pkgdesc="Logstash shipping tool using the lumberjack protocol"
arch=('x86_64')
url="https://github.com/elasticsearch/logstash-forwarder"
license=('APACHE')
depends=('glibc')
makedepends=('go')
backup=(etc/logstash-forwarder.conf)
source=('git+https://github.com/elasticsearch/logstash-forwarder.git'
        'logstash-forwarder.service'
        'logstash-forwarder-journal.service'
        'logstash-forwarder-journalctl')

pkgver() {
	cd "$srcdir/logstash-forwarder"
	git describe --tags --always | sed 's|-|.|g'
}

build() {
	cd "$srcdir/logstash-forwarder"
	make PREFIX=/usr
}

package() {
	cd "$srcdir/logstash-forwarder"
	install -dm755 "$pkgdir"/var/lib/logstash-forwarder
	install -Dm644 logstash-forwarder.conf.example "$pkgdir"/etc/logstash-forwarder.conf
	install -Dm755 build/bin/logstash-forwarder "$pkgdir"/usr/bin/logstash-forwarder
	install -Dm644 "$srcdir"/logstash-forwarder.service "$pkgdir"/usr/lib/systemd/system/logstash-forwarder.service
	install -Dm644 "$srcdir"/logstash-forwarder-journal.service "$pkgdir"/usr/lib/systemd/system/logstash-forwarder-journal.service
	install -Dm0755 "$srcdir/logstash-forwarder-journalctl" "$pkgdir/usr/lib/systemd/scripts/logstash-forwarder-journalctl"
}

sha256sums=('SKIP'
            'c2346140c185d21a1216f834395340fb9c5024c604d40e19666502e7dafde836'
            '3dcd72c2ff09851daf94be4b76984d2063610acdceeb71a65530a0bd2fac5b6f'
            '024b7f7859eaba144950b3cc2d9a0a97694ec99babc30c421490ba25f9012139')
