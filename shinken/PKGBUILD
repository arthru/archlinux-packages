# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Julien "Adyxax" Dessaux <judessaux@gmail.com>

pkgname=shinken
pkgver=2.0_RC2
pkgrel=1
pkgdesc='An open source Nagios® like tool, redesigned and rewritten from scratch. Its main goal is to meet today’s system monitoring requirements while still following compatibility to Nagios®'
arch=('any')
url='http://www.shinken-monitoring.org/'
license=('GPL3')
depends=('python2-pyro')
makedepends=('python2-setuptools' 'python2-sphinx')
optdepends=('nagios-plugins: various monitoring checks'
            'nagcon: a simple yet great curses interface over nagios style status.dat file'
            'python-pysqlite: for the livestatus module'
            'python-simplejson: for the livestatus module')
install='shinken.install'
source=("https://pypi.python.org/packages/source/S/Shinken/Shinken-${pkgver//_/-}.tar.gz"
        'shinken.install'
        'shinken.logrotate'
        'shinken.tmpfiles'
        'shinken-arbiter.service'
        'shinken-broker.service'
        'shinken-poller.service'
        'shinken-reactionner.service'
        'shinken-receiver.service'
        'shinken-scheduler.service'
        'setup.cfg')

prepare() {
	cd "$srcdir/Shinken-${pkgver//_/-}"
	find -name '*.py' -exec sed -i 's|^#!/usr/bin/env python2.6$|#!/usr/bin/env python2.7|' {} \;
}

build() {
	cd "$srcdir/Shinken-${pkgver//_/-}"
	cp -f "$srcdir/setup.cfg" .
	python2 setup.py build

	msg2 "Building docs"
	cd doc
	sphinx-build2 -b man -d build/doctrees/ source/ build/man/
}

package() {
	cd "$srcdir/Shinken-${pkgver//_/-}"

	python2 setup.py install --skip-build -O1 --root="$pkgdir"

	cd "$pkgdir"
	install -dm0755 var/lib/shinken/rw

	# man
	install -dm0755 usr/share/man
	mv var/lib/shinken/doc/build/man usr/share/man/man1

	# cleanup
	rm -rf var/lib/shinken/doc etc/init.d

	cd "$srcdir"

	# logrotate
	install -Dm0644 shinken.logrotate "$pkgdir/etc/logrotate.d/shinken"

	# systemd
	install -Dm0644 shinken.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/shinken.conf"
	for service in *.service; do
	    install -Dm0644 $service "$pkgdir/usr/lib/systemd/system/$service"
	done
}

sha256sums=('a14381e18a0ed9f21ad3f809c46a17b7334b26aa3bdebc2d41e008e132266b96'
            '9c7ae7a0187cbaebeaac5629fa44666f1dace35800617bdf2d70cf2f23ee8e97'
            'fb460d2c790093d3737dea62ac632df80293a58c983e8f837b968f5c3490cdd5'
            'e0f4017bd5f270909cd668279b64f09a73674ead3f1d0e29f11ec392397b9b71'
            'efa31d6bac681f6e994f5a0544ecaa5ab044099057d5e75d2e6b41e9e69f361f'
            'b59d662720ca67a10d72b2ee4103331241aa67e55d577c3a65738114d7225de9'
            '1efccc4d13388f38408a28d15715cef5fae8efc35d6a579960f05f6ee1ca3f33'
            '6bd19e2098d356a043dbb1f62f675c7c2e6dc34f8d60f0eff30c332d07e9e092'
            '9a792277402ab8a5fb6bb44f3ae26986576cc343ce69030a760aacb44b84b44e'
            '14185ae2ae2bbe9ab0260f1b2fd820e16a40adeadfe9149753d28f8645ac91e6'
            'e58d8b4244f4995c463f10a5975854cdaaf5c27b55a23a47ef1a81f02fdda2c0')
