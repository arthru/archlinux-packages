# $Id: PKGBUILD 99951 2013-10-31 02:39:14Z allan $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Vladimir Tsanev <tsachev@gmail.com>

pkgname=hiredis
pkgver=0.11.0
pkgrel=3
pkgdesc='Minimalistic C client library for Redis'
arch=('x86_64' 'i686')
url="https://github.com/redis/hiredis/"
depends=('glibc')
makedepends=('redis')
license=('BSD')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/redis/$pkgname/tar.gz/v$pkgver)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

check() {
 cd "$srcdir/$pkgname-$pkgver"
 sed -r 's|echo \\|echo -e \\|' -i Makefile
 make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX="$pkgdir/usr" install
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

sha256sums=('ff7b2849e55bf3589eecced7125934feb9645c36a4d490d001dc08c93553eafd')
