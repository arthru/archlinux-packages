# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=xtrabackup
pkgver=2.1.6
pkgrel=2
pkgdesc="A MySQL non-blocking backup solution for InnoDB and XtraDB (Percona Server) data."
arch=("i686" "x86_64")
url="http://www.percona.com/software/percona-xtrabackup/"
license=('GPL')
depends=("perl" "libaio" "libgcrypt")

source=("http://www.percona.com/downloads/XtraBackup/XtraBackup-$pkgver/source/percona-$pkgname-$pkgver.tar.gz"
    build.patch)

prepare() {
    cd "$srcdir/percona-$pkgname-$pkgver"
    patch -p0 -i "$srcdir/build.patch"
}

build() {
    cd "$srcdir/percona-$pkgname-$pkgver"
    export CFLAGS="$CFLAGS -Wno-sizeof-pointer-memaccess -Wno-maybe-uninitialized -Wno-unused -Wno-array-bounds -Wno-error"
    export CXXFLAGS="$CXXFLAGS -Wno-sizeof-pointer-memaccess -Wno-maybe-uninitialized -Wno-unused -Wno-array-bounds -Wno-error"
    utils/build.sh innodb56
}

package() {
	cd "$srcdir/percona-$pkgname-$pkgver"
    for f in xtrabackup_56 xbcrypt xbstream; do
        install -Dm0755 src/$f "$pkgdir"/usr/bin/$f
    done
    install -Dm0755 innobackupex "$pkgdir"/usr/bin/innobackupex
    install -Dm0644 doc/xtrabackup.1 "$pkgdir"/usr/share/man/man1/xtrabackup.1
}

sha256sums=('40af775cc818bb45ef9c49bc24eb414742005ee20c2474f9895dff6649351a79'
            '05fc1ba879d2d32512919c790c8cfd7abbfe0261c947825c9597aac7b482c384')
